// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    Inc16(in=rOut, out=incOut);

    Mux16(a=rOut, b=incOut, sel=inc, out=m1);
    Mux16(a=m1, b=in, sel=load, out=m2);
    Mux16(a=m2, b=false, sel=reset, out=m3);

    // calculate should load or not
    Or(a=inc, b=load, out=tmp1);
    Or(a=tmp1, b=reset, out=loadEnable);

    Register(in=m3, load=loadEnable, out=rOut, out=out);
}